#!/bin/zsh

# where vim and tmux should be
vim="/usr/local/bin/vim"
tmux="/usr/local/bin/tmux"

# if clientserver exists, we're getting fancy...
if [[ $($vim -h | grep serverlist) != "" ]]; then

  # get the name of this vim server from the pwd
  servername=$(echo $(basename $(pwd)) | tr "[:lower:]" "[:upper:]")

  # keep track of where vim server is only if tmux is active
  if [[ $TMUX != "" ]]; then

    # Make sure this hash exists
    typeset -A vimservers
    db="$HOME/.vimservers"
    if [[ -f $db ]]; then
      source $db
    fi

    # vim server exists; go to it
    if [[ $($vim --serverlist) == *$servername* ]]; then

      # tmux over there!
      if [[ $vimservers[$servername] != "" ]]; then
        $tmux switch-client -t $vimservers[$servername]
      fi

    # vim server doesn't exist
    else
      # save the current tmux session:window.pane under this servername
      vimservers[$servername]="$(tmux display-message -p '#S:#I.#P')"
      echo "vimservers=(${(kv)vimservers})" >| $db

      # set the pane title to vim
      printf '\033]2;vim\033\\'
    fi

  fi
fi

# process args
arg_is_cmd=0
cmd=""
files=()
args=()

for arg in "$@"; do

  # catch any -c commands
  if [[ $arg_is_cmd == 1 ]]; then
    cmd=$arg
    arg_is_cmd=0
  else

    # if the arg is -c, the next one will be a -c command
    if [[ $arg == "-c" ]]; then
      arg_is_cmd=1

    # catch arguments that are a single hyphen
    elif [[ $arg == "-" ]]; then

    # catch arguments that are a double hyphen
    elif [[ $arg == "--" ]]; then

    # catch other arguments that start with a hyphen
    elif [[ $arg == "-"* ]]; then
      args+=($arg)

    # otherwise, it's a file to open
    else 
      files+=($arg) 
    fi

  fi
done

# if there's no files, edit a blank "new" file 
if [[ ${#files} == 0 ]]; then
  files+=("new")
fi

# fire up vim
if [[ $servername != "" ]]; then

  # open files in remote vim tabs; creating or attaching to the vim server
  $vim --servername $servername -c "$cmd" $args --remote-tab-silent $files

# no vim server?
else
  # set the pane title to vim
  printf '\033]2;vim\033\\'

  # open files in vim tabs without a vim server
  $vim -c "$cmd" $args -p $files
fi

